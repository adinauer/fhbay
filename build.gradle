def springVersion = '3.1.2.RELEASE'

allprojects {
	group = 'at.dinauer.fhbay'
	version = '0.1-SNAPSHOT'
}

subprojects {
	apply plugin: 'java'
	apply plugin: 'eclipse'

	repositories {
		mavenLocal()
		mavenCentral()
	}

	configurations {
		provided { transitive = true }
		compile  { transitive = true }
	}

	sourceSets {
		main.compileClasspath += configurations.provided
		test.compileClasspath += configurations.provided
		test.runtimeClasspath += configurations.provided
	}

	dependencies {
		testCompile 'junit:junit:4.11'
		testCompile 'org.hamcrest:hamcrest-all:1.3'
	}
}

project(':fhbay-commons') {
	dependencies {
		provided 'org.hibernate.javax.persistence:hibernate-jpa-2.0-api:1.0.1.Final'
		provided 'org.jboss.spec.javax.ejb:jboss-ejb-api_3.1_spec:1.0.2.Final'
	}
}

project(':fhbay-server') {
	dependencies {
		compile project(':fhbay-commons')

		provided 'org.jboss.spec.javax.annotation:jboss-annotations-api_1.1_spec:1.0.1.Final'
		provided 'org.hibernate.javax.persistence:hibernate-jpa-2.0-api:1.0.1.Final'
		provided 'org.jboss.spec.javax.ejb:jboss-ejb-api_3.1_spec:1.0.2.Final'
		provided 'org.jboss.spec.javax.jms:jboss-jms-api_1.1_spec:1.0.1.Final'
	}
}

project(':fhbay-client-commons') {
	dependencies {
		compile project(':fhbay-commons')

		compile 'joda-time:joda-time:2.2'
		runtime 'org.hibernate:hibernate-core:4.1.9.Final'
		runtime 'org.jboss.spec.javax.ejb:jboss-ejb-api_3.1_spec:1.0.2.Final'
		runtime 'org.jboss:jboss-ejb-client:1.0.2.Final'
		runtime 'org.jboss.xnio:xnio-api:3.0.3.GA'
		runtime 'org.jboss.xnio:xnio-nio:3.0.3.GA'
		compile 'org.jboss.sasl:jboss-sasl:1.0.0.Final'

		provided 'javax.servlet:servlet-api:2.5'
		provided 'javax.servlet:jstl:1.2'

		testCompile 'mysql:mysql-connector-java:5.1.26'
	}
}

project(':fhbay-web') {
	apply plugin: 'war'

	dependencies {
		compile project(':fhbay-client-commons')
		provided project(':fhbay-commons')

		provided 'javax.servlet:servlet-api:2.5'
		provided 'javax.servlet:jstl:1.2'

		compile 'joda-time:joda-time:2.2'
		compile "org.springframework:spring-webmvc:$springVersion"
		compile "org.springframework.security:spring-security-core:$springVersion"
		compile "org.springframework.security:spring-security-web:$springVersion"
		compile "org.springframework.security:spring-security-config:$springVersion"
		compile "org.springframework.security:spring-security-taglibs:$springVersion"
		compile "org.springframework:spring-aop:$springVersion"
		compile 'org.aspectj:aspectjrt:1.6.11'
		compile 'org.aspectj:aspectjweaver:1.6.11'
		compile 'cglib:cglib:2.2'
		compile 'org.codehaus.jackson:jackson-mapper-asl:1.9.12'
		compile 'commons-collections:commons-collections:3.2.1'
	}
}

project(':fhbay-ear') {
	apply plugin: 'ear'

	dependencies {
		deploy project(':fhbay-commons')
		deploy project(':fhbay-server')
		deploy project(path:':fhbay-web', configuration:'archives')
	}
}